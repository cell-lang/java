type SimplyNamedInterType = BasicInterType, TaggedInterType[<generic>], NamedInterType;

type DeclInterType   = TupleInterType, RecordInterType, NamedInterType, UnionInterType, TaggedInterType[UnionableInterType];



implicit typedefs : [TypeName -> NeType] {
  ([InterType -> String], [InterType -> String], [String*], [String*]) declared_types([NeType] types) {
    inter_types = [interface_type(t) : t <- types];
    decl_types = union([types_to_declare(t) : t <- inter_types]);
    reserved_names = []; ## IMPLEMENT
    let reserved_names = reserved_names:
      types_names_input  = interface_type_names(decl_types, host_to_cell = true);
      types_names_output = interface_type_names(decl_types, host_to_cell = false);
    ;
    let input_types_names = types_names_input, output_types_names = types_names_output, parents = []:
      input_types_decls  = [t -> type_decl(t, s, false, host_to_cell = true)  : t, s <- types_names_input];
      output_types_decls = [t -> type_decl(t, s, false, host_to_cell = false) : t, s <- types_names_output];
    ;

    loop
      conflicts = union([{
          in_name  = types_names_input(t, !!);
          out_name = types_names_output(t, !!);
          in_decl  = input_types_decls(t, !!);
          out_decl = output_types_decls(t, !!);
          return [t if in_name == out_name and in_decl != out_decl];
        } : t <- decl_types
      ]);

      break if conflicts == [];

      reserved_names = reserved_names & [types_names_input(t, !!) : t <- conflicts];
      let reserved_names = reserved_names:
        types_names_output = interface_type_names(decl_types, host_to_cell = false);
      ;
      let input_types_names = types_names_input, output_types_names = types_names_output,
          parents = [], host_to_cell = false:
        output_types_decls = [t -> type_decl(t, s, false) : t, s <- types_names_output];
      ;
    ;

    let input_types_names = types_names_input, output_types_names = types_names_output, parents = []:
      input_types_docs  = [t -> type_decl(t, s, true, host_to_cell = true)  : t, s <- types_names_input];
      output_types_docs = [t -> type_decl(t, s, true, host_to_cell = false) : t, s <- types_names_output];
    ;

    type_decls = values(input_types_decls) & values(output_types_decls);
    type_docs  = values(input_types_docs)  & values(output_types_docs);

print (types_names_input, types_names_output, type_decls, type_docs);

    return (types_names_input, types_names_output, type_decls, type_docs);
  }


  // [DeclInterType] types_to_declare(SymbolInterType inter_type) = [inter_type];
  // [DeclInterType] types_to_declare(SymbolInterType inter_type) = [];

  [DeclInterType] types_to_declare(InterType inter_type) =
    bool            |
    integer         |
    float           |
    symbol          |
    string          |
    generic         = [],
    tuple(ts?)      = [inter_type] & union([types_to_declare(t) : t <~ ts]),
    record(fs?)     = [inter_type] & union([types_to_declare(t) : l, t <- fs]),
    seq(t?)         |
    set(t?)         = types_to_declare(t),
    bin_rel(ts?)    |
    tern_rel(ts?)   = union([types_to_declare(t) : t <~ ts]),
    tagged(_, t?)   = types_to_declare(t),
    named(_, rt?)   = [inter_type] & #([t : t <- types_to_declare(rt)] - [strip_tags(rt)]),
    union(ts?)      = [inter_type] & ts & union([types_to_declare(t) : t <- ts]) - [strip_tags(t) : t <- ts];
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit host_to_cell : Bool, reserved_names : [String] {
  [DeclInterType -> String] interface_type_names([DeclInterType] decl_types) {
    default_type_names = [t -> safe_name(t, nothing) : t <- decl_types];
    conflicting_types = values(group_by(keys_with_non_unique_values(default_type_names), default_type_names($, !!)));
    renamed_types = [t -> safe_name(t, just(i)) : ts <- conflicting_types, t @ i <~ isort(ts)];
    return [t -> lookup(renamed_types, t, default_type_names(t, !!)) : t <- decl_types];
  }


  String safe_name(TupleInterType type, Maybe[Nat] unique_id) {
    suffix = if unique_id != nothing then "_" & printed(value(unique_id)) else "";
    field_types = untag(type);
    name = if field_types :: SimplyNamedInterType+
      then append((field_type_name(t) : t <- field_types), "_")
      else "Tuple";
    return non_reserved_name(name & suffix);

    String field_type_name(SimplyNamedInterType) =
      bool                = "Bool",
      integer             = "Long",
      float               = "Double",
      symbol              = "Symbol",
      string              = "String",
      generic             |
      tagged(_, generic)  = if host_to_cell then "String" else "Value",
      named(s?, _)        = upper_camel_case(s);
  }


  String safe_name(RecordInterType type, Maybe[Nat] unique_id) {
    suffix = if unique_id != nothing then "_" & printed(value(unique_id)) else "";
    field_labels = isort(keys(untag(type)));
    base_name = append((upper_camel_case(l) : l <- field_labels), "_");
    return non_reserved_name(base_name & suffix);
  }


  String safe_name(NamedInterType type, Maybe[Nat] unique_id) {
    suffix = if unique_id != nothing then "_" & printed(value(unique_id)) else "";
    type_symb = match (type) named(s?, _) = s;
    return non_reserved_name(upper_camel_case(type_symb) & suffix);
  }


  String safe_name(TaggedInterType[UnionableInterType] type, Maybe[Nat] unique_id) {
    suffix = if unique_id != nothing then "_" & printed(value(unique_id)) else "";
    tag = match (type) tagged(s?, _) = s;
    return non_reserved_name(upper_camel_case(tag) & suffix);
  }


  String safe_name(UnionInterType type, Maybe[Nat] unique_id) {
    suffix = if unique_id != nothing then "_" & printed(value(unique_id)) else "";
    return non_reserved_name("TypeUnion" & suffix);
  }


  String non_reserved_name(String unsafe_name) {
    name = unsafe_name;
    while reserved_names(name):
      name = "_" & name;
    ;
    return name;
  }
}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

implicit host_to_cell : Bool, input_types_names : [InterType -> String],
         output_types_names : [InterType -> String], parents : [InterType -> String]
{
  String* type_decl(TupleInterType type, String name, Bool doc_only) {
    types = (if host_to_cell then input_arg_type(t) else return_value_type(t) : t @ i <- untag(type));
    max_len = max((length(t) : t <- types));
    spaces = (string((ascii_space : i < max_len - length(t))) : t <- types);
    body = ("  public " & t & spaces(i) & " item" & printed(i) & ";" : t @ i <- types);
    header = #(if doc_only then "" else "public static ") & "class " & name & lookup(parents, type, "") & " {";
    return (header) & body & ("}");
  }


  String* type_decl(RecordInterType type, String name, Bool doc_only) {
    fields = [l -> if host_to_cell then input_arg_type(t) else return_value_type(t) : l, t <- untag(type)];
    field_names = java_adjusted_unambiguous_lower_camel_case_names(keys(fields));
    max_len = max([length(t) : unused_var, t <- fields]);
    body = ({
        spaces = string((ascii_space : unused_var < max_len - length(t)));
        return "  public " & t & spaces & " " & field_names(l, !!) & ";";
      } : l, t <- isort(fields)
    );
    header = #(if doc_only then "" else "public static ") & "class " & name & lookup(parents, type, "") & " {";
    return (header) & body & ("}");
  }


  String* type_decl(NamedInterType type, String name, Bool doc_only) {
    return match (type)
      named(_, t?) = nameable_type_decl(t, name, doc_only);

    String* nameable_type_decl(<TupleInterType, RecordInterType> type, String name, Bool doc_only) =
      type_decl(type, name, doc_only);

    String* nameable_type_decl(TaggedInterType[NameableInterType] type, String name, Bool doc_only) =
      tagged(_, t?) = nameable_type_decl(t, name, doc_only);
  }


  String* type_decl(TaggedInterType[UnionableInterType] type, String name, Bool doc_only) {
    return match (type)
      tagged(_, t?) = type_decl(t, name, doc_only);
  }


  String* type_decl(UnionInterType type, String name, Bool doc_only) = (
    "interface " & name & " {",
    "",
    "}"
  );
}


[Symbol -> String] java_adjusted_unambiguous_lower_camel_case_names([Symbol] symbols) {
  nice_field_names = [s -> safe_lower_camel_case(s, false) : s <- symbols];
  conflicting_names = duplicate_values(nice_field_names);
  return [
    l -> if conflicting_names(n) then safe_lower_camel_case(l, true) else n
    : l, n <- nice_field_names
  ];
}

////////////////////////////////////////////////////////////////////////////////

[InterType -> String] java_type_names([NamedInterType] types) = [
  ## THIS SHOULD ALSO RESOLVE CONFLICTS WITH OTHER UPPER CAMEL CASE NAMES
  t -> upper_camel_case(s) : type <- types, named(s?, t?) ?= type
];


String safe_lower_camel_case(Symbol symb) = safe_lower_camel_case(symb, false);


String safe_lower_camel_case(Symbol symb, Bool keep_underscore) {
  name = lower_camel_case(symb);
  name = "_" & name if in(name, java_reserved_identifiers);
  return name;
}

String safe_lower_camel_case_chain(Symbol+ symbs) {
  name = append((lower_camel_case(s) : s <- symbs), "_");
  name = "_" & name if in(name, java_reserved_identifiers);
  return name;
}

////////////////////////////////////////////////////////////////////////////////

InterType strip_tags(InterType type) =
  tagged(_, t?) = strip_tags(t),
  _             = type;

////////////////////////////////////////////////////////////////////////////////

[V] duplicate_values([K -> V] map) = duplicates((v : unused_var, v <- isort(map)));

[K] keys_with_non_unique_values([K -> V] map) {
  dup_vals = duplicate_values(map);
  return [k : k, v <- map, dup_vals(v)];
}

[K -> V] remove_keys_with_non_unique_values([K -> V] map) {
  keys_to_remove = keys_with_non_unique_values(map);
  return [k -> v : k, v <- map, not keys_to_remove(k)];
}

////////////////////////////////////////////////////////////////////////////////

[String] java_reserved_identifiers = [
  "abstract",
  "assert",
  "boolean",
  "break",
  "byte",
  "case",
  "catch",
  "char",
  "class",
  "const",
  "continue",
  "default",
  "do",
  "double",
  "else",
  "enum",
  "extends",
  "false",
  "final",
  "finally",
  "float",
  "for",
  "goto",
  "if",
  "implements",
  "import",
  "instanceof",
  "int",
  "interface",
  "long",
  "native",
  "new",
  "null",
  "package",
  "private",
  "protected",
  "public",
  "return",
  "short",
  "static",
  "strictfp",
  "super",
  "switch",
  "synchronized",
  "this",
  "throw",
  "throws",
  "transient",
  "true",
  "try",
  "void",
  "volatile",
  "while"
];


[String] runtime_classes = [
  "Algs",
  "BinaryTable",
  "BinaryTableUpdater",
  "BinRelIter",
  "BlankObj",
  "Builder",
  "ByteStream",
  "Conversions",
  "EmptyRelObj",
  "EmptyRelValue",
  "FloatObj",
  "FloatValue",
  "Hacks",
  "Index",
  "IntObj",
  "IntValue",
  "Iter",
  "Lexer",
  "MasterSeqObj",
  "Miscellanea",
  "NeBinRelObj",
  "NeBinRelValue",
  "NeSetObj",
  "NeSetValue",
  "NeTernRelObj",
  "NeTernRelValue",
  "NullObj",
  "Obj",
  "OneWayBinTable",
  "OverflowTable",
  "Pair",
  "Parser",
  "Parsing",
  "ParsingException",
  "Procs",
  "SeqObj",
  "SeqOrSetIter",
  "SeqValue",
  "SliceObj",
  "SymbObj",
  "SymbTable",
  "SymbValue",
  "TaggedObj",
  "TaggedValue",
  "TernaryTable",
  "TernaryTableUpdater",
  "TernRelIter",
  "Token",
  "TokenStream",
  "Triplet",
  "UnaryTable",
  "UnaryTableUpdater",
  "ValueBase",
  "ValueStore",
  "ValueStoreBase",
  "ValueStoreUpdater",
  "WrappingUtils"
];
